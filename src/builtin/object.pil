import "object.pil"
import "io.pil"
import "parser.pil"
import "ast/ast.pil"

import diesler
import diesler::exception

DSLClass diesler::builtin::object::init(Scope scope) {
  var object = new DSLClass(scope, null, "Object");

  object.methods["new"] = new diesler::builtin::object::NewInstanceMethod(scope);
  object.methods["defineInstance:"] = new diesler::builtin::object::DefineMethods(scope, true);
  object.methods["define:"] = new diesler::builtin::object::DefineMethods(scope, false);
  object.methods["subClass:with:"] = new diesler::builtin::object::SubclassMethod(scope);
  object.methods["importNativeClass:"] = new diesler::builtin::native::NewNativeClassMethod(scope);
  object.methods["methods"] = new diesler::builtin::object::MethodsMethod(scope);
  object.methods["instanceMethods"] = new diesler::builtin::object::InstanceMethodsMethod(scope);

  object.instanceMethods["isA:"] = new diesler::builtin::object::IsAMethod(scope);
  object.instanceMethods["import:"] = new diesler::builtin::object::ImportMethod(scope);

  object.instanceMethods["=="] = new diesler::builtin::object::EqMethod(scope);
  object.instanceMethods["!="] = new diesler::builtin::object::NotEqMethod(scope);

  object.instanceMethods["set:to:"] = new diesler::builtin::object::SetFieldMethod(scope);
  object.instanceMethods["get:"] = new diesler::builtin::object::GetFieldMethod(scope);
  object.instanceMethods["describe"] = new diesler::builtin::object::DescribeMethod(scope);
  object.instanceMethods["define:"] = new diesler::builtin::object::DefineMethods(scope, false);
  object.instanceMethods["methods"] = new diesler::builtin::object::MethodsMethod(scope);

  object.instanceMethods["print:"] = new diesler::builtin::object::PrintMethod(scope);
  object.instanceMethods["if:then:"] = new diesler::builtin::object::IfThenMethod(scope);
  object.instanceMethods["if:then:else:"] = new diesler::builtin::object::IfThenElseMethod(scope);
  object.instanceMethods["while:do:"] = new diesler::builtin::object::WhileMethod(scope);

  object.instanceMethods["try:catch:"] = new diesler::builtin::object::TryCatchMethod(scope);
  object.instanceMethods["raise:"] = new diesler::builtin::object::RaiseExceptionMethod(scope);
  return object;
}

class diesler::builtin::object::NewInstanceMethod extends DSLMethod {
  new(Scope scope) extends super(scope, "new", new Array<ASTPattern>(), null) {
    this.cls = cls;
  }

  DSLObject invoke(Scope scope, DSLObject o, Array<DSLObject> args) {
    var obj = new DSLObject(o.as<DSLClass>);
    if(obj.getMethod("init") != null) {
      obj.getMethod("init").invoke(scope, obj, new Array<DSLMethod>());
    }
    return obj;
  }
}

class diesler::builtin::object::SetFieldMethod extends DSLMethod {
  new(Scope scope) extends super(scope, "set:to:", new Array<ASTPattern>(new ASTIdnPattern(scope, "fieldName"), new ASTIdnPattern(scope, "value")), null) {
  }

  DSLObject invoke(Scope scope, DSLObject o, Array<DSLObject> args) {
    var fieldName = args[0].as<DSLString>.value;
    o.setField(fieldName, args[1]);
    return o;
  }
}

class diesler::builtin::object::GetFieldMethod extends DSLMethod {
  new(Scope scope) extends super(scope, "get:", new Array<ASTPattern>(new ASTIdnPattern(scope, "fieldName")), null) {
  }

  DSLObject invoke(Scope scope, DSLObject o, Array<DSLObject> args) {
    var fieldName = args[0].as<DSLString>.value;
    return o.getField(fieldName);
  }
}

class diesler::builtin::object::DescribeMethod extends DSLMethod {
  new(Scope scope) extends super(scope, "describe", new Array<ASTPattern>(), null) {
  }

  DSLObject invoke(Scope scope, DSLObject o, Array<DSLObject> args) {
    return new DSLString(scope, o.as<String>);
  }
}

class diesler::builtin::object::InstanceMethodsMethod extends DSLMethod {
  new(Scope scope) extends super(scope, "instanceMethods", new Array<ASTPattern>(), null) {
  }

  DSLObject invoke(Scope scope, DSLObject o, Array<DSLObject> args) {
    var l = new List<DSLObject>();
    var c = o.as<DSLClass>;
    for(String methodName : c.instanceMethods.keys) {
      l.add(c.instanceMethods[methodName]);
    }
    if(c.superClass != null) {
      DSLList superMethods = c.superClass.methods["instanceMethods"].invoke(scope, c.superClass, args).as<DSLList>;
      for(DSLMethod m : superMethods.items) {
        l.add(m);
      }
    }
    return new DSLList(scope, l);
  }
}

class diesler::builtin::object::MethodsMethod extends DSLMethod {
  new(Scope scope) extends super(scope, "methods", new Array<ASTPattern>(), null) {
  }

  DSLObject invoke(Scope scope, DSLObject o, Array<DSLObject> args) {
    var l = new List<DSLObject>();
    for(String methodName : o.methods.keys) {
      l.add(o.methods[methodName]);
    }
    if(o.cls != null) {
      DSLList classMethods = o.cls.getMethod("instanceMethods").invoke(scope, o.cls, args).as<DSLList>;
      for(DSLMethod m : classMethods.items) {
        l.add(m);
      }
    }
    return new DSLList(scope, l);
  }
}

class diesler::builtin::object::PrintMethod extends DSLMethod {
  new(Scope scope) extends super(scope, "print:", new Array<ASTPattern>(new ASTIdnPattern(scope, "str")), null) {
  }

  DSLObject invoke(Scope scope, DSLObject o, Array<DSLObject> args) {
    println(args[0].as<String>);
    return o;
  }
}

class diesler::builtin::object::DefineMethods extends DSLMethod {
  Bool instanceMethods = true;

  new(Scope scope, Bool instanceMethods) extends super(scope, "define:", new Array<ASTPattern>(new ASTIdnPattern(scope, "methods")), null) {
    this.instanceMethods = instanceMethods;
  }

  DSLObject invoke(Scope scope, DSLObject self, Array<DSLObject> args) {
    DSLMethodDefs defs = args[0].as<DSLMethodDefs>;
    for(ASTMethodDef mdt : defs.declaredMethods) {
      var newMethod = new DSLMethod(scope, mdt.signature.selector, mdt.signature.arguments, new ASTBlock(scope, mdt.signature.arguments, mdt.statements));
      if(self isa DSLClass && instanceMethods) {
        self.as<DSLClass>.instanceMethods[mdt.signature.selector] = newMethod;
      } else {
        self.methods[mdt.signature.selector] = newMethod;
      }
    }
    return self;
  }
}

class diesler::builtin::object::SubclassMethod extends DSLMethod {
  new(Scope scope) extends super(scope, "subClass:with:", new Array<ASTPattern>(new ASTIdnPattern(scope, "className"), new ASTIdnPattern(scope, "methods")), null) {
  }

  DSLObject invoke(Scope scope, DSLObject self, Array<DSLObject> args) {
    var className = args[0].as<DSLString>.value;
    var definitions = args[1].as<DSLMethodDefs>; 
    var cls = new DSLClass(scope, self.as<DSLClass>, className);
    for(ASTMethodDef mdt : definitions.declaredMethods) {
      var newMethod = new DSLMethod(scope, mdt.signature.selector, mdt.signature.arguments, new ASTBlock(scope, mdt.signature.arguments, mdt.statements));
      cls.instanceMethods[mdt.signature.selector] = newMethod;
    }
    return cls;
  }
}

class diesler::builtin::object::IsAMethod extends DSLMethod {
  new(Scope scope) extends super(scope, "isA:", new Array<ASTPattern>(new ASTIdnPattern(scope, "class")), null) {
  }

  DSLObject invoke(Scope scope, DSLObject self, Array<DSLObject> args) {
    return new DSLBool(scope, self.isInstanceOf(args[0].as<DSLClass>));
  }
}

class diesler::builtin::object::EqMethod extends DSLMethod {
  new(Scope scope) extends super(scope, "==", new Array<ASTPattern>(new ASTIdnPattern(scope, "other")), null) {
  }

  DSLObject invoke(Scope scope, DSLObject o, Array<DSLObject> args) {
    return new DSLBool(scope, o == args[0]);
  }
}

class diesler::builtin::object::NotEqMethod extends DSLMethod {
  new(Scope scope) extends super(scope, "!=", new Array<ASTPattern>(new ASTIdnPattern(scope, "other")), null) {
  }

  DSLObject invoke(Scope scope, DSLObject o, Array<DSLObject> args) {
    return new DSLBool(scope, !(o == args[0]));
  }
}

class diesler::builtin::object::IfThenMethod extends DSLMethod {
  new(Scope scope) extends super(scope, "if:then:", new Array<ASTPattern>(new ASTIdnPattern(scope, "condition"), new ASTIdnPattern(scope, "ifTrue")), null) {
  }

  DSLObject invoke(Scope scope, DSLObject o, Array<DSLObject> args) {
    verifyType(args[0], typeof DSLMethod);
    verifyType(args[1], typeof DSLMethod);
    DSLMethod cond = args[0].as<DSLMethod>;
    DSLMethod trueBlock = args[1].as<DSLMethod>;
    DSLObject value = cond.invoke(scope, o, new Array<DSLObject>());
    if(!(value isa DSLBool)) {
      throw new Exception("Condition should return a Bool: " + cond.as<String>);
    }
    if(value.as<DSLBool>.value) {
      return trueBlock.invoke(scope, o, new Array<DSLObject>());
    }
    return o;
  }
}

class diesler::builtin::object::IfThenElseMethod extends DSLMethod {
  new(Scope scope) extends super(scope, "if:then:else:", new Array<ASTPattern>(new ASTIdnPattern(scope, "condition"), new ASTIdnPattern(scope, "ifTrue"), new ASTIdnPattern(scope, "ifFalse")), null) {
  }

  DSLObject invoke(Scope scope, DSLObject o, Array<DSLObject> args) {
    verifyType(args[0], typeof DSLMethod);
    verifyType(args[1], typeof DSLMethod);
    verifyType(args[2], typeof DSLMethod);
    DSLMethod cond = args[0].as<DSLMethod>;
    DSLMethod trueBlock = args[1].as<DSLMethod>;
    DSLMethod falseBlock = args[2].as<DSLMethod>;
    DSLObject value = cond.invoke(scope, o, new Array<DSLObject>());
    if(!(value isa DSLBool)) {
      throw new Exception("Condition should return in bool: " + cond.as<String>);
    }
    if(value.as<DSLBool>.value) {
      return trueBlock.invoke(scope, o, new Array<DSLObject>());
    } else {
      return falseBlock.invoke(scope, o, new Array<DSLObject>());
    }
  }
}

class diesler::builtin::object::WhileMethod extends DSLMethod {
  new(Scope scope) extends super(scope, "while:do:", new Array<ASTPattern>(new ASTIdnPattern(scope, "condition"), new ASTIdnPattern(scope, "block")), null) {
  }

  DSLObject invoke(Scope scope, DSLObject o, Array<DSLObject> args) {
    verifyType(args[0], typeof DSLMethod);
    verifyType(args[1], typeof DSLMethod);
    DSLMethod cond = args[0].as<DSLMethod>;
    DSLMethod block = args[1].as<DSLMethod>;
    DSLObject value = cond.invoke(scope, o, new Array<DSLObject>());
    if(!(value isa DSLBool)) {
      throw new Exception("Condition should return in bool: " + cond.as<String>);
    }
    while(value.as<DSLBool>.value) {
      block.invoke(scope, o, new Array<DSLObject>());
      if(scope.hasReturned) {
        return scope.returnValue;
      }
      value = cond.invoke(scope, o, new Array<DSLObject>());
    }
    return o;
  }
}

class diesler::builtin::object::TryCatchMethod extends DSLMethod {
  new(Scope scope) extends super(scope, "try:catch:", new Array<ASTPattern>(new ASTIdnPattern(scope, "tryBlock"), new ASTIdnPattern(scope, "catchBlock")), null) {
  }

  DSLObject invoke(Scope scope, DSLObject o, Array<DSLObject> args) {
    DSLMethod tryBlock = args[0].as<DSLMethod>;
    DSLMethod catchBlock = args[1].as<DSLMethod>;
    Scope tryScope = new Scope(scope, o);
    try {
      tryBlock.invoke(tryScope, o, args);
    } catch(DieslerException e) {
      Scope catchScope = new Scope(scope, o);
      catchBlock.invoke(catchScope, o, new Array<DSLObject>(e.obj));
    }
    return null;
  }
}

class diesler::builtin::object::RaiseExceptionMethod extends DSLMethod {
  new(Scope scope) extends super(scope, "raise:", new Array<ASTPattern>(new ASTIdnPattern(scope, "exception")), null) {
  }

  DSLObject invoke(Scope scope, DSLObject o, Array<DSLObject> args) {
    throw new DieslerException(args[0]);
  }
}

class diesler::builtin::object::ImportMethod extends DSLMethod {
  new(Scope scope) extends super(scope, "import:", new Array<ASTPattern>(new ASTIdnPattern(scope, "filename")), null) {
  }

  DSLObject invoke(Scope scope, DSLObject o, Array<DSLObject> args) {
    var file = new pil::io::ReadFile(args[0].as<DSLString>.value);
    var content = file.readAll();
    file.close();
    ASTBlock block = new Parser(scope, content).acceptStatements();
    return block.eval(scope);
  }
}
